config {
  type: "table",
  schema: "predictions",
  name: "bgg_game_coordinates",
  description: "Game UMAP/PCA coordinates for embedding visualization, sourced from bgg-predictive-models. Only includes coordinates from the latest embedding version."
}

WITH latest_version AS (
  SELECT MAX(embedding_version) as version
  FROM ${ref("bgg-predictive-models", "raw", "game_coordinates")}
)

SELECT * EXCEPT(rn)
FROM (
  SELECT
    game_id,
    umap_1,
    umap_2,
    pca_1,
    pca_2,
    embedding_model,
    embedding_version,
    created_ts,
    ROW_NUMBER() OVER (PARTITION BY game_id ORDER BY created_ts DESC) as rn
  FROM ${ref("bgg-predictive-models", "raw", "game_coordinates")}
  WHERE embedding_version = (SELECT version FROM latest_version)
)
WHERE rn = 1
