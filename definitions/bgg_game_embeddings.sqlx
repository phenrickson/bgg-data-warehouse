config {
  type: "incremental",
  schema: "predictions",
  name: "bgg_game_embeddings",
  uniqueKey: ["game_id"],
  description: "Latest game embeddings for similarity search, sourced from bgg-predictive-models. Only includes embeddings from the latest model version to ensure consistent vector space."
}

-- Get the latest embedding version to ensure all embeddings are from the same model
WITH latest_version AS (
  SELECT MAX(embedding_version) as version
  FROM ${ref("bgg-predictive-models", "raw", "game_embeddings")}
),

source_data AS (
  SELECT
    game_id,
    name,
    year_published,
    embedding,
    embedding_8,
    embedding_16,
    embedding_32,
    embedding_model,
    embedding_version,
    embedding_dim,
    algorithm,
    created_ts,
    job_id,
    ROW_NUMBER() OVER (PARTITION BY game_id ORDER BY created_ts DESC, job_id DESC) as rn
  FROM ${ref("bgg-predictive-models", "raw", "game_embeddings")}
  WHERE embedding_version = (SELECT version FROM latest_version)
    ${when(incremental(), `AND created_ts > (SELECT MAX(created_ts) FROM ${self()})`) }
)

SELECT * EXCEPT(rn)
FROM source_data
WHERE rn = 1
